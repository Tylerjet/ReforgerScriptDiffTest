[EntityEditorProps(category: "GameScripted/Utilities", description: "Displays points generated by the SCR_SpherePointGenerator")]
class SCR_SpherePointGeneratorPreviewComponentClass : ScriptComponentClass
{
}

class SCR_SpherePointGeneratorPreviewComponent : ScriptComponent
{
	[Attribute(desc: "Radii of spheres to generate", defvalue: "{}", category: "Sphere", params: "0.001 inf")]
	ref array<float> m_aSphereRadii;

	[Attribute(desc: "Size of generated points", defvalue: "1", category: "Sphere", params: "0.001 inf")]
	float m_fPointSize;

	[Attribute(desc: "Maximum generated points", defvalue: "1000", category: "Sphere", params: "0 100000")]
	int m_iMaxPoints;

	[Attribute(desc: "Spacing between generated points. ", defvalue: "5", category: "Spacing Based", params: "0.001 inf")]
	float m_fSpacing;

	[Attribute(desc: "Amount of Points to generate in each sphere. Overrides Spacing Based settings.", defvalue: "{}", category: "Amount Based", params: "0 100000")]
	ref array<int> m_aPointAmounts;

	[Attribute(desc: "Base color", defvalue: "", category: "Labelling")]
	ref Color m_BaseColor;

	[Attribute(desc: "Hue-shift By Point In Sphere", defvalue: "1", category: "Labelling")]
	bool m_bHueShiftByIndex;

	[Attribute(desc: "Hue-shift By Sphere", defvalue: "1", category: "Labelling")]
	bool m_bHueShiftBySphere;

	[Attribute(desc: "Show index", defvalue: "1", category: "Labelling")]
	bool m_bShowIndex;

	protected string m_sInstanceId;

	protected static const int PREVIEW_POINT_BUDGET = 100000;
	protected static ref set<string> s_KeyUpdateSet;

	//------------------------------------------------------------------------------------------------
	//! \param[in] owner The owner represents the entity being initialized in the method, which is used to set event mask and clear debug elements.
	override protected void OnPostInit(IEntity owner)
	{
		DrawPreview();
	}

	//------------------------------------------------------------------------------------------------
	//! Draws preview spheres with varying radii, colors, and indices on the specified position.
	//! Should be called on creation or init.
	protected void DrawPreview()
	{
		string tag = string.Format("SCR_SpherePointGeneratorPreview_%1", GetInstanceId());
		SCR_DebugShapeHelperComponent.RemoveTag(tag);
		if (!IsActive())
			return;

		int sphereAmount = m_aSphereRadii.Count();
		if (sphereAmount < 1)
			return;

		if (!m_BaseColor)
			m_BaseColor = Color.FromInt(SCR_DebugShapeHelperComponent.MakeTransparent(Color.VIOLET));
		if (m_fSpacing < 0.01)
			m_fSpacing = 0.01;

		World world = GetOwner().GetWorld();
		vector position = GetOwner().GetOrigin();
		SCR_ColorAHSV colorHSVA = SCR_ColorAHSV.FromColor(m_BaseColor);
		int colorInt = m_BaseColor.PackToInt();
		float pointShiftOffset = 0;
		float pointShiftModifier = 0.833; // Prevents full hue rotation to avoid confusion.
		if (m_bHueShiftBySphere)
			pointShiftModifier /= sphereAmount;

		int remainingPointBudget = Math.Min(m_iMaxPoints, PREVIEW_POINT_BUDGET);
		for (int sphereI = 1; sphereI <= sphereAmount; ++sphereI)
		{
			int pointsAmount;
			float radius = m_aSphereRadii[sphereI - 1];
			if (radius <= 0)
				continue;

			if (m_aPointAmounts.Count() >= sphereI)
				pointsAmount = m_aPointAmounts[sphereI - 1];
			else
				pointsAmount = SCR_SpherePointGenerator.EstimatePointsFromSpacingOnUnitSphere(m_fSpacing / radius);

			if (pointsAmount < 0)
				continue; // Allow other sheres with defiend points to be renderd.

			pointsAmount = Math.Min(pointsAmount, remainingPointBudget);
			remainingPointBudget -= pointsAmount;
			if (pointsAmount < 1)
				break; // No more points in budget.

			if (m_bHueShiftBySphere)
				pointShiftOffset = (sphereI - 1) / sphereAmount;
			for (int pointI = 0; pointI < pointsAmount; ++pointI)
			{
				int shiftedColor = colorInt;
				if (m_bHueShiftByIndex)
					shiftedColor = colorHSVA.WithHueShift(pointShiftOffset + pointShiftModifier * (pointI / pointsAmount)).ToColor().PackToInt();
				vector point = position + radius * SCR_SpherePointGenerator.GetPointOnUnitSphereFromEquator(pointsAmount, pointI);
				SCR_DebugShapeHelperComponent.AddSphere(point, m_fPointSize, shiftedColor, tag: tag);
				if (world && m_bShowIndex)
					SCR_DebugShapeHelperComponent.AddText(world, point, pointI.ToString(), tag: tag);
			}
		}
	}

	//------------------------------------------------------------------------------------------------
	//! The name of the entity this component is in. By Default its empty unless named by mission creator.
	//! Duplicates would just result in only one SCR_SpherePointGeneratorPreviewComponent being drawn.
	//! \return Instance ID usable for SCR_DebugShapeHelperComponent tags.
	protected string GetInstanceId()
	{
		if (!SCR_StringHelper.IsEmptyOrWhiteSpace(m_sInstanceId))
			return m_sInstanceId;
		m_sInstanceId = GetOwner().GetName();
		return m_sInstanceId;
	}

	//------------------------------------------------------------------------------------------------
	//! Draws debug preview shapes in Workbench.
	override void _WB_OnInit(IEntity owner, inout vector mat[4], IEntitySource src)
	{
		DrawPreview();
		super._WB_OnInit(owner, mat, src);
	}

	//------------------------------------------------------------------------------------------------
	//! Any property value has been changed. You can use editor API here and do some additional edit actions which will be part of the same "key changed" action.
	override bool _WB_OnKeyChanged(IEntity owner, BaseContainer src, string key, BaseContainerList ownerContainers, IEntity parent)
	{
		if (key == "coords")
		{
			DrawPreview();
			return true;
		}

		if (GetKeyUpdateSet().Contains(key))
			return false;

		return true;
	}

	//------------------------------------------------------------------------------------------------
	//! Called when Entity is being to be destroyed (deleted) or component to be deleted (see Game::DeleteScriptComponent).
	//! param[in] owner Entity which owns the component
	override void OnDelete(IEntity owner)
	{
		SCR_DebugShapeHelperComponent.RemoveTag("SCR_SpherePointGeneratorPreview_" + GetInstanceId());
	}

	//------------------------------------------------------------------------------------------------
	//! \return A set of keys for updating properties.
	protected static set<string> GetKeyUpdateSet()
	{
		if (!s_KeyUpdateSet)
		{
			s_KeyUpdateSet = new set<string>();
			s_KeyUpdateSet.Insert("Enabled");
			s_KeyUpdateSet.Insert("m_aSphereRadii");
			s_KeyUpdateSet.Insert("m_fPointSize");
			s_KeyUpdateSet.Insert("m_iMaxPoints");
			s_KeyUpdateSet.Insert("m_fSpacing");
			s_KeyUpdateSet.Insert("m_aPointAmounts");
			s_KeyUpdateSet.Insert("m_BaseColor");
			s_KeyUpdateSet.Insert("m_bHueShiftByIndex");
			s_KeyUpdateSet.Insert("m_bHueShiftBySphere");
			s_KeyUpdateSet.Insert("m_bShowIndex");
		}
		return s_KeyUpdateSet;
	}
}
