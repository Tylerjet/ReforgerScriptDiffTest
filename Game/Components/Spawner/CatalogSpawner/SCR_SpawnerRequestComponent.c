//------------------------------------------------------------------------------------------------
class SCR_SpawnerRequestComponentClass : ScriptGameComponentClass
{
};

//------------------------------------------------------------------------------------------------
//! Used for handling entity spawning requests for SCR_EntitySpawnerComponent and inherited classes, attached to SCR_PlayerController
class SCR_SpawnerRequestComponent : ScriptComponent
{
	static const float NOTIFICATION_DURATION = 2;

	//------------------------------------------------------------------------------------------------
	//! Defender spawner request from SCR_EnableDefendersAction
	//! \param defenderSpawnerComp DefenderSpawner component to be handled
	//! \param enable bool to enable or disable unit spawning
	//! \param playerID id of player requesting the spawn
	void EnableSpawning(notnull SCR_DefenderSpawnerComponent defenderSpawnerComp, bool enable, int playerID)
	{
		IEntity spawnerOwnerEntity = defenderSpawnerComp.GetOwner();
		if (!spawnerOwnerEntity)
			return;

		RplComponent spawnerRplComp = RplComponent.Cast(spawnerOwnerEntity.FindComponent(RplComponent));
		if (!spawnerRplComp)
			return;

		Rpc(RPC_DoEnableSpawning, spawnerRplComp.Id(), enable, playerID);
	}

	//------------------------------------------------------------------------------------------------
	//! Performs request on server
	//! \param rplCompId RplComp id of entity with SCR_DefenderSpawnerComponent
	//! \param index item index in User Faction
	//! \param playerID id of player requesting the spawn
	[RplRpc(RplChannel.Reliable, RplRcver.Server)]
	void RPC_DoEnableSpawning(RplId defenderSpawnerID, bool enable, int playerID)
	{
		RplComponent rplComp = RplComponent.Cast(Replication.FindItem(defenderSpawnerID));
		if (!rplComp)
			return;

		SCR_DefenderSpawnerComponent entitySpawnerComp = SCR_DefenderSpawnerComponent.Cast(rplComp.GetEntity().FindComponent(SCR_DefenderSpawnerComponent));
		if (!entitySpawnerComp)
			return;

		entitySpawnerComp.EnableSpawning(enable, playerID);
	}

	//------------------------------------------------------------------------------------------------
	//! Entity spawn request from SCR_SpawnEntityUserAction
	//! \param index item index in User Action
	//! \param spawnerComponent SpawnerComponent on which should be entity spawned
	//! \param User requesting spawn
	//! \param slot on which should be entity spawned
	void RequestCatalogEntitySpawn(int index, notnull SCR_CatalogEntitySpawnerComponent spawnerComponent, IEntity user, SCR_EntitySpawnerSlotComponent slot)
	{
		int userId = GetGame().GetPlayerManager().GetPlayerIdFromControlledEntity(user);
		if (userId == 0)
			return;

		IEntity spawnerOwnerEntity = spawnerComponent.GetOwner();
		if (!spawnerOwnerEntity)
			return;

		RplComponent spawnerRplComp = RplComponent.Cast(spawnerOwnerEntity.FindComponent(RplComponent));
		if (!spawnerRplComp)
			return;

		RplComponent slotRplComp = RplComponent.Cast(slot.GetOwner().FindComponent(RplComponent));
		if (!slotRplComp)
			return;

		Rpc(RPC_DoRequestCatalogSpawn, spawnerRplComp.Id(), index, userId, slotRplComp.Id());
	}

	//------------------------------------------------------------------------------------------------
	//! Performs request on server
	//! \param rplCompId RplComp id of entity with spawner component
	//! \param index item index in User Action
	//! \param userId id of user requesting spawn
	//! \param slotRplId slot RplID OPTIONAL
	[RplRpc(RplChannel.Reliable, RplRcver.Server)]
	void RPC_DoRequestCatalogSpawn(RplId rplCompId, int index, int userId, RplId slotRplId)
	{
		RplComponent rplComp = RplComponent.Cast(Replication.FindItem(rplCompId));
		if (!rplComp)
			return;

		SCR_CatalogEntitySpawnerComponent entitySpawnerComp = SCR_CatalogEntitySpawnerComponent.Cast(rplComp.GetEntity().FindComponent(SCR_CatalogEntitySpawnerComponent));
		if (!entitySpawnerComp)
			return;

		SCR_EntityCatalogEntry entityEntry = entitySpawnerComp.GetEntryAtIndex(index);
		if (!entityEntry)
			return;

		RplComponent slotRplComp = RplComponent.Cast(Replication.FindItem(slotRplId));
		if (!slotRplComp)
			return;

		SCR_EntitySpawnerSlotComponent slotComp = SCR_EntitySpawnerSlotComponent.Cast(slotRplComp.GetEntity().FindComponent(SCR_EntitySpawnerSlotComponent));
		if (!slotComp)
			return;

		entitySpawnerComp.InitiateSpawn(entityEntry, userId, slotComp);
	}

	//------------------------------------------------------------------------------------------------
	//! Send notification to player
	void SendPlayerFeedback(int msgID, int assetIndex, int catalogType = -1)
	{
		Rpc(RPC_DoPlayerFeedbackImpl, msgID, assetIndex, catalogType);
	}

	//------------------------------------------------------------------------------------------------
	//! Show notification about request result to the requester
	[RplRpc(RplChannel.Reliable, RplRcver.Owner)]
	protected void RPC_DoPlayerFeedbackImpl(int msgID, int assetIndex, int catalogType)
	{
		string msg;
		string msg2;

		if (msgID == SCR_EEntityRequestStatus.NOT_ENOUGH_SPACE)
		{
			msg = "#AR-Campaign_DeliveryPointObstructed-UC";
		}

		SCR_PlayerController player = SCR_PlayerController.Cast(GetGame().GetPlayerController());
		if (!player)
			return;

		SCR_Faction faction = SCR_Faction.Cast(player.GetLocalControlledEntityFaction());
		if (!faction)
			return;

		SCR_EntityCatalog factionCatalog = faction.GetFactionEntityCatalogOfType(catalogType);
		if (!factionCatalog)
			return;

		SCR_EntityCatalogEntry entry = factionCatalog.GetCatalogEntry(assetIndex);
		if (!entry)
			return;

		msg = GetMessageStringForCatalogType(catalogType);

		SCR_EntityCatalogSpawnerData spawnerData = SCR_EntityCatalogSpawnerData.Cast(entry.GetEntityDataOfType(SCR_EntityCatalogSpawnerData));
		if (spawnerData)
			msg2 = spawnerData.GetOverwriteName();
		
		if (msg2 == string.Empty)
			msg2 = entry.GetEntityName();

		SCR_PopUpNotification.GetInstance().PopupMsg(msg, NOTIFICATION_DURATION, msg2);
	}

	//------------------------------------------------------------------------------------------------
	//! Return string ID to be used in succesfull request feedbacks.
	//! \param catalogType catalog type from enum.
	string GetMessageStringForCatalogType(EEntityCatalogType catalogType)
	{
		switch (catalogType)
		{
			case EEntityCatalogType.VEHICLE:
			{
				return "#AR-Campaign_VehicleReady-UC";
			}
			case EEntityCatalogType.CHARACTER:
			{
				return "#AR-Campaign_UnitReady-UC";
			}
			case EEntityCatalogType.GROUP:
			{
				return "#AR-Campaign_GroupReady-UC";
			}
		}

		//No specific catalogType defined
		return "#AR-Campaign_AssetReady-UC";
	}

	//------------------------------------------------------------------------------------------------
	override void OnPostInit(IEntity owner)
	{
		if (!GetGame().InPlayMode())
			return;
	}
};
