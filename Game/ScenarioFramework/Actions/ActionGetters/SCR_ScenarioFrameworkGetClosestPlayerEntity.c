[BaseContainerProps(), SCR_ContainerActionTitle()]
class SCR_ScenarioFrameworkGetClosestPlayerEntity : SCR_ScenarioFrameworkGet
{
	[Attribute(desc: "Closest to what - use getter")]
	ref SCR_ScenarioFrameworkGet m_Getter;
	
	[Attribute(desc: "(Optional) You can filter players by putting the Faction Key of desired faction(s). If not used, all factions will be eligible")]
	ref array<string> m_aFactionKeys;

	//------------------------------------------------------------------------------------------------
	override SCR_ScenarioFrameworkParamBase Get()
	{
		if (!m_Getter)
		{
			Print("ScenarioFramework: GetClosestPlayerEntity - The object the distance is calculated from is missing!", LogLevel.ERROR);
			return null;
		}

		SCR_ScenarioFrameworkParam<IEntity> entityWrapper = SCR_ScenarioFrameworkParam<IEntity>.Cast(m_Getter.Get());
		if (!entityWrapper)
			return null;

		IEntity entityFrom = entityWrapper.GetValue();
		if (!entityFrom)
			return null;

		array<int> playerIDs = {};
		GetGame().GetPlayerManager().GetPlayers(playerIDs);

		IEntity closestEntity;
		IEntity entityToBeChecked;
		SCR_ChimeraCharacter chimeraCharacter;
		float fDistance = float.MAX;
		bool factionKeyIsEmpty = m_aFactionKeys.IsEmpty();
		foreach (int playerID : playerIDs)
		{
			entityToBeChecked = GetGame().GetPlayerManager().GetPlayerControlledEntity(playerID);
			if (!entityToBeChecked)
				continue;
			
			if (!factionKeyIsEmpty)
			{
				chimeraCharacter = SCR_ChimeraCharacter.Cast(entityToBeChecked);
				if (!chimeraCharacter || !m_aFactionKeys.Contains(chimeraCharacter.GetFactionKey()))
					continue;
			}

			float fActualDistance = vector.DistanceSqXZ(entityFrom.GetOrigin(), entityToBeChecked.GetOrigin());

			if (fActualDistance < fDistance)
			{
				closestEntity = entityToBeChecked;
				fDistance = fActualDistance;
			}
		}

		if (!closestEntity)
			return null;

		return new SCR_ScenarioFrameworkParam<IEntity>(closestEntity);
	}
}