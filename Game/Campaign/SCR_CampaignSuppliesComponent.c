[EntityEditorProps(category: "GameScripted/Campaign", description: "Makes a vehicle able to carry Campaign resources.", color: "0 0 255 255")]
class SCR_CampaignSuppliesComponentClass: ScriptComponentClass
{
};

//------------------------------------------------------------------------------------------------
//! Makes a vehicle able to carry Campaign resources
class SCR_CampaignSuppliesComponent : ScriptComponent
{
	// Member variables 
	protected SCR_CampaignMilitaryBaseComponent m_LastLoadedAt;
	protected SCR_CampaignMilitaryBaseComponent m_LastUnloadedAt;
	protected SCR_CampaignMilitaryBaseComponent m_LastXPAwardedAt;
	protected bool m_bAwardUnloadXP = true;
	protected bool m_bIsPlayerInRange;
	
	static const float SUPPLY_TRUCK_UNLOAD_RADIUS = 25;			//m: maximum distance from a supply depot a player can still (un)load their truck
	
	[RplProp()]
	protected ref array<int> m_aLoadingPlayerIDs = {};
	
	[RplProp()]
	protected ref array<int> m_aUnloadingPlayerIDs = {};
	
	// Script Invoker
	ref ScriptInvoker m_OnSuppliesChanged = new ScriptInvoker();
	ref ScriptInvoker m_OnSuppliesTruckDeleted = new ScriptInvoker();
	
	// Synced variables
	[Attribute("0", desc: "How many supplies this component holds when it is created."), RplProp(onRplName: "OnSuppliesChanged")]
	protected int m_iSupplies;
	
	// Synced variables
	[Attribute("0", desc: "Maximum supplies this component can hold."), RplProp(onRplName: "OnSuppliesChanged")]
	protected int m_iSuppliesMax;
	
	[Attribute("0", desc: "Maximum distance from a supply depot a player can still (un)load their truck")]
	protected float m_fOperationalRadius;
	
	[Attribute("0", UIWidgets.CheckBox, "This component belongs to a supply depot without a parent base.", "")]
	bool m_bIsStandaloneDepot;
	
	//------------------------------------------------------------------------------------------------
	// Returns first player from loading/unloading arrays 
	// Use True parameters, to return from Unloading array. Otherwise, it will look in loading players
	int GetLoadingPlayer(bool unloading = false)
	{
		array<int> loadingArray;
		//Switch between loading and unloading players
		if(!unloading)
			loadingArray = m_aLoadingPlayerIDs;
		else
			loadingArray = m_aUnloadingPlayerIDs;
		
		//return id of first player (if there is any)
		if(!loadingArray.IsEmpty())
			return loadingArray[0];	
		else
			return 0;
	}
	//------------------------------------------------------------------------------------------------
	// Setter for Loading players array
	void SetSupplyLoadingPlayer(int playerID)
	{
		if(!m_aLoadingPlayerIDs.Contains(playerID))
		{
			m_aLoadingPlayerIDs.Insert(playerID);
			Replication.BumpMe();
		}
	}
	
	//------------------------------------------------------------------------------------------------
	// Deletes from loading players array
	void DeleteSupplyLoadingPlayer(int playerID)
	{
		if(m_aLoadingPlayerIDs.Contains(playerID))
		{
			m_aLoadingPlayerIDs.RemoveItemOrdered(playerID);
			Replication.BumpMe();
		}
	}
	
	//------------------------------------------------------------------------------------------------
	//~ Called on player killed
	protected void OnPlayerKilled(int playerID, IEntity player = null, IEntity killer = null)
	{
		DeleteSupplyLoadingPlayer(playerID);
		DeleteSupplyUnloadingPlayer(playerID);
	}
	
	//------------------------------------------------------------------------------------------------
	// Setter for Unloading players array
	void SetSupplyUnloadingPlayer(int playerID)
	{
		if(!m_aUnloadingPlayerIDs.Contains(playerID))
		{
			m_aUnloadingPlayerIDs.Insert(playerID);
			Replication.BumpMe();
		}
	}
	
	//------------------------------------------------------------------------------------------------
	// Deletes from Unloading players array
	void DeleteSupplyUnloadingPlayer(int playerID)
	{
		if(m_aUnloadingPlayerIDs.Contains(playerID))
		{
			m_aUnloadingPlayerIDs.RemoveItemOrdered(playerID);
			Replication.BumpMe();
		}
	}
	
	//------------------------------------------------------------------------------------------------
	void OnSuppliesChanged()
	{
		// script invoker executed when ammout of supplies in vehicle changed. m_iSupplies is ammout of currently loaded supplies.
		m_OnSuppliesChanged.Invoke(m_iSupplies, m_iSuppliesMax);
	}
	
	//------------------------------------------------------------------------------------------------
	int GetSupplies()
	{
		return m_iSupplies;
	}
	
	//------------------------------------------------------------------------------------------------
	int GetSuppliesMax()
	{
		return m_iSuppliesMax;
	}
	
	//------------------------------------------------------------------------------------------------
	void SetSuppliesMax(int suppliesMax)
	{
		m_iSuppliesMax = suppliesMax;
		if (m_iSupplies > suppliesMax)
			m_iSupplies = m_iSuppliesMax;
		
		Replication.BumpMe();
		OnSuppliesChanged();
	}
	
	//------------------------------------------------------------------------------------------------
	// Get the load / unload radius. If set custom use this. if it's set to zero, use default.
	float GetOperationalRadius()
	{
		if (m_fOperationalRadius != 0)
			return m_fOperationalRadius;
		
		return SUPPLY_TRUCK_UNLOAD_RADIUS;
	}
	
	//------------------------------------------------------------------------------------------------
	void SetIsPlayerInRange(bool status)
	{
		m_bIsPlayerInRange = status;
	}
	
	//------------------------------------------------------------------------------------------------
	bool GetIsPlayerInRange()
	{
		return m_bIsPlayerInRange;
	}
	
	//------------------------------------------------------------------------------------------------
	void SetLastLoadedAt(SCR_CampaignMilitaryBaseComponent base)
	{
		m_LastLoadedAt = base;
		
		if (m_LastLoadedAt == m_LastUnloadedAt && m_LastUnloadedAt == m_LastXPAwardedAt)
			m_bAwardUnloadXP = false;
		else
			m_bAwardUnloadXP = true;
	}
	
	//------------------------------------------------------------------------------------------------
	void SetLastUnloadedAt(SCR_CampaignMilitaryBaseComponent base)
	{
		m_LastUnloadedAt = base;
		
		if (m_LastLoadedAt == m_LastUnloadedAt)
			m_bAwardUnloadXP = false;
		else
			m_LastXPAwardedAt = base;
	}
	
	//------------------------------------------------------------------------------------------------
	bool AwardXP()
	{
		return m_bAwardUnloadXP;
	}
		
	//------------------------------------------------------------------------------------------------
	void AddSupplies(int supplies, bool replicate = true)
	{
		int oldSupplies = m_iSupplies;
		m_iSupplies += supplies;
		
		if (m_iSupplies > m_iSuppliesMax)
			m_iSupplies = m_iSuppliesMax;

		if (replicate && m_iSupplies != oldSupplies)
			Replication.BumpMe();
		
		OnSuppliesChanged();
	}
	
	//------------------------------------------------------------------------------------------------
	bool GetIsStandaloneDepot()
	{
		return m_bIsStandaloneDepot;
	}
	//------------------------------------------------------------------------------------------------
	override void EOnInit(IEntity owner)
	{
		SCR_BaseGameMode gamemode = SCR_BaseGameMode.Cast(GetGame().GetGameMode());
		if (!gamemode)
			return;
		
		gamemode.GetOnPlayerDisconnected().Insert(DeleteSupplyLoadingPlayer);
		gamemode.GetOnPlayerDisconnected().Insert(DeleteSupplyUnloadingPlayer);
		gamemode.GetOnPlayerKilled().Insert(OnPlayerKilled);
		
		if (m_bIsStandaloneDepot && GetGame().InPlayMode())
		{
			SCR_GameModeCampaign campaign = SCR_GameModeCampaign.GetInstance();
			
			if (campaign)
				campaign.GetBaseManager().RegisterRemnantSupplyDepot(this);
		}
	}
	
	//------------------------------------------------------------------------------------------------
	override void OnPostInit(IEntity owner)
	{
		super.OnPostInit(owner);
		SetEventMask(owner, EntityEvent.INIT);
		
		if (m_iSupplies > m_iSuppliesMax)
		{
			string err = string.Format("SCR_CampaignSuppliesComponent on %1 carries more supplies (%2) than its maximum (%3)!", owner, m_iSupplies, m_iSuppliesMax);
			Print(err, LogLevel.ERROR);
		}
	}
	
	//------------------------------------------------------------------------------------------------
	//~  Get supplies component on given entity. Returns null if no supplies can be found
	static SCR_CampaignSuppliesComponent GetSuppliesComponent(notnull IEntity ent)
	{
		SCR_CampaignSuppliesComponent suppliesComponent;
		
		suppliesComponent = SCR_CampaignSuppliesComponent.Cast(ent.FindComponent(SCR_CampaignSuppliesComponent));
		if (suppliesComponent)
			return suppliesComponent;
		
		SlotManagerComponent slotManager = SlotManagerComponent.Cast(ent.FindComponent(SlotManagerComponent));	
		if (!slotManager)
			return null;
	
		array<EntitySlotInfo> slots = {};
		slotManager.GetSlotInfos(slots);
		IEntity truckBed;
		
		foreach (EntitySlotInfo slot: slots)
		{
			if (!slot)
				continue;
			
			truckBed = slot.GetAttachedEntity();
			
			if (!truckBed)
				continue;
			
			suppliesComponent = SCR_CampaignSuppliesComponent.Cast(truckBed.FindComponent(SCR_CampaignSuppliesComponent));
			if (suppliesComponent)
				return suppliesComponent;
		}
		
		return null;
	}
	
	//------------------------------------------------------------------------------------------------
	// Constructor
	void SCR_CampaignSuppliesComponent(IEntityComponentSource src, IEntity ent, IEntity parent)
	{
	}

	//------------------------------------------------------------------------------------------------
	// Destructor
	void ~SCR_CampaignSuppliesComponent()
	{
		if (m_OnSuppliesTruckDeleted)
			m_OnSuppliesTruckDeleted.Invoke(GetOwner());
		
		SCR_BaseGameMode gamemode = SCR_BaseGameMode.Cast(GetGame().GetGameMode());
		if (!gamemode)
			return;
		
		gamemode.GetOnPlayerDisconnected().Remove(DeleteSupplyLoadingPlayer);
		gamemode.GetOnPlayerDisconnected().Remove(DeleteSupplyUnloadingPlayer);
		gamemode.GetOnPlayerKilled().Remove(OnPlayerKilled);
	}
};