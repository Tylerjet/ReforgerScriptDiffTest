//------------------------------------------------------------------------------------------------
//! To prevent fetching data for each condition multiple times within a single frame,
//! we use this container instead to pass around data where needed.
//! TODO: Possibly reuse for other HUD needs to save some perf?
class SCR_AvailableActionsConditionData
{
	protected ChimeraCharacter m_CharacterEntity;
	protected CharacterControllerComponent m_ControllerComponent;
	protected CharacterInputContext m_CharacterInputConxtext
	
	//! Whether data is valid at current moment, FetchData has to be called prior to this to have it updated
	protected bool m_bIsValid;
	
	//! Current character stance
	protected ECharacterStance m_eCharacterStance;
	// Type of current compartment
	protected ECompartmentType m_eCompartmentType;
	//! Is character aiming down sights?
	protected bool m_bIsCharacterADS;
	//! Is character seated in a vehicle?
	protected bool m_bIsCharacterInVehicle;
	//! Is character getting in?
	protected bool m_bIsCharacterGettingIn;
	//! Is character getting out?
	protected bool m_bIsCharacterGettingOut;
	//! Is character sprinting?
	protected bool m_bIsCharacterSprinting;
	//! Is character swimming?
	protected bool m_bIsCharacterSwimming;
	//! Is character falling? (airborne)
	protected bool m_bIsCharacterFalling;
	//! Is character reloading 
	protected bool m_bIsCharacterReloading;
	//! Can character get out of vehicle?
	protected bool m_bCanCharacterGetOutVehicle;
	//! Is character weapon raised
	protected bool m_bIsCharacterWeaponRaised;
	//! Can character fire with weapon 
	protected bool m_bCanCharacterFireWeapon;
	//! Is character using an item at the moment
	protected bool m_bIsCharacterUsingItem;
	//! Is character bleeding
	protected bool m_bIsCharacterBleeding;
	//! Is inventory open
	protected bool m_bIsInventoryOpen;
	//! Is  quick slot bar available
	protected bool m_bIsQuickSlotAvailable;
	//! Is weapon quick bar shown
	protected bool m_bIsQuickSlotShown;
	//! Is gadget selection mode active
	protected bool m_bIsGadgetSelection;
	//! Is character in focus mode
	protected float m_fFocusMode;
	//! How long is character bleeding 
	protected float m_fBleedingTime;
	//! How long is character sprinting 
	protected float m_fSprintingTime;
	//! Number of additional magazines character has for current weapon
	protected int m_iAdditionalMagazines;
	//! Number of medial items
	protected int m_iMedicalItemCount;
	//! Number of medical items in quick slots
	protected int m_iMedicalItemCountInQuickSlots;
	//! Currently equipped item entity (if any)
	protected IEntity m_pCurrentItemEntity;
	//! Current character weapon entity (if any)
	protected IEntity m_pCurrentWeaponEntity;
	//! Current weapon
	protected BaseWeaponComponent m_pCurrentWeapon;
	//! Current weapon muzzle
	protected BaseMuzzleComponent m_pCurrentMuzzle;
	//! Current weapon magazine
	protected BaseMagazineComponent m_pCurrentMagazine;
	//! Current controlled vehicle
	protected IEntity m_pCurrentVehicle;
	//! How long is vehicle using turbo
	protected float m_fTurboTime;
	
	// Von variables ----------------------
	//! Is character using radio 
	protected bool m_bCharacterIsUsingRadio;
	//! Count of available radios 
	protected bool m_bCharacterRadiosCount;
	
	protected BaseControllerComponent m_pCurrentVehicleController;
	//! Current controlled vehicle signals
	protected SignalsManagerComponent m_pCurrentVehicleSignals;
	//! Current controlled vehicle weapon 
	protected BaseWeaponComponent m_pCurrentVehicleWeapon;
	
	//! First data call
	protected bool m_bIsSetted = false;
	//! Force immediate update of conditions
	protected bool m_IsForcedUpdate = false;
	//! Inventory fetching limitation
	protected bool m_bCanFetchInventory = true;
	
	//------------------------------------------------------------------------------------------------
	private void OnItemAddedListener( IEntity item, BaseInventoryStorageComponent storage )
	{
		m_bCanFetchInventory = true;
	}

	private void OnItemRemovedListener( IEntity item, BaseInventoryStorageComponent storage )
	{
		m_bCanFetchInventory = true;
	}

	private void OnInventoryOpen(bool open)
	{
		if (open)
		{
			SCR_InventoryAvailableCondition.IncrementCounter();
		}
		m_bIsInventoryOpen = open;
	}

	private void OnQuickSlotOpen(bool open)
	{
		if (open)
		{
			SCR_WeaponQuickSlotAvailableCondition.IncrementCounter();
		}
		m_bIsQuickSlotAvailable = open;
		m_bIsQuickSlotShown = open;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Clears all variables by setting them to their default state
	//! Also invalidates this data
	private void Clear()
	{
		m_eCharacterStance = ECharacterStance.STAND;
		m_eCompartmentType = ECompartmentType.Pilot;
		m_bIsCharacterADS = false;
		m_bIsCharacterInVehicle = false;
		m_bIsCharacterGettingIn = false;
		m_bIsCharacterGettingOut = false;
		m_bIsCharacterSwimming = false;
		m_bIsCharacterSprinting = false;
		m_bIsCharacterFalling = false;
		m_bIsCharacterReloading = false;
		m_bIsCharacterWeaponRaised = false;
		m_bCanCharacterFireWeapon = false;
		m_bIsCharacterUsingItem = false;
		m_bIsGadgetSelection = false;
		m_fFocusMode = 0.0;
		//m_iAdditionalMagazines = 0;
		m_pCurrentItemEntity = null;
		m_pCurrentWeaponEntity = null;
		m_pCurrentWeapon = null;
		m_pCurrentMuzzle = null;
		m_pCurrentMagazine = null;
		m_pCurrentVehicle = null;
		m_pCurrentVehicleSignals = null;
		m_pCurrentVehicleWeapon = null;

		m_bIsValid = false;
	}
	
	/* Gadgets magic */
	protected SCR_GadgetManagerComponent m_GadgetManager;
	
	//------------------------------------------------------------------------------------------------
	private void GetAvailableGadgets()
	{
		m_GadgetManager = SCR_GadgetManagerComponent.GetGadgetManager( SCR_PlayerController.GetLocalControlledEntity() );
		m_bIsGadgetSelection = GetGame().GetInputManager().GetActionTriggered("GadgetSelection");
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns held gadget or null if none
	IEntity GetHeldGadget()
	{
		if (m_GadgetManager)
			return m_GadgetManager.GetHeldGadget();
		return null;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns held gadget component or null if none
	SCR_GadgetComponent GetHeldGadgetComponent()
	{
		if (m_GadgetManager)
			return m_GadgetManager.GetHeldGadgetComponent();
		return null;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Return held gadget being aimed with
	bool GetGadgetRaised()
	{
		if (m_GadgetManager)
			return m_GadgetManager.GetIsGadgetADS();
		return null;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns gadget by type or null if none
	IEntity GetGadget(EGadgetType type)
	{
		if (m_GadgetManager)
			return m_GadgetManager.GetGadgetByType(type);
		return null;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns current magazine or null if none
	BaseMagazineComponent GetCurrentMagazine()
	{
		return m_pCurrentMagazine;
	}

	//------------------------------------------------------------------------------------------------
	//! Returns current weapon or none if null
	BaseMuzzleComponent GetCurrentMuzzle()
	{
		return m_pCurrentMuzzle;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns current weapon or none if null
	BaseWeaponComponent GetCurrentWeapon()
	{
		return m_pCurrentWeapon;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns current character stance
	ECharacterStance GetCharacterStance()
	{
		return m_eCharacterStance;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns current compartment type
	ECompartmentType GetCompartmentType()
	{
		return m_eCompartmentType;
	}
	
	//------------------------------------------------------------------------------------------------	
	//! Returns <0,1> of focus mode amount; 0 = none, 1 = full focus
	float GetFocusModeAmount()
	{
		return m_fFocusMode;
	}
	
	//------------------------------------------------------------------------------------------------	
	//! Returns for how long is character bleeding
	float GetCharacterBleedingTime()
	{
		return m_fBleedingTime;
	}
	
	//------------------------------------------------------------------------------------------------	
	//! Returns for how long is character bleeding
	float GetCharacterSprintingTime()
	{
		return m_fSprintingTime;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns count of additional available magazines for current weapon
	//! TODO: MuzzleInMag :)))))
	int GetAdditionalMagazinesCount()
	{
		return m_iAdditionalMagazines;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns count of medial items 
	int GetMedicalItemCount()
	{
		return m_iMedicalItemCount;
	}
	
	//------------------------------------------------------------------------------------------------
	int GetMedicalItemCountInQuickSlots()
	{
		return m_iMedicalItemCountInQuickSlots;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns whether character is aiming down sights or not
	bool GetIsCharacterInVehicle()
	{
		return m_bIsCharacterInVehicle;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns whether character is getting in
	bool GetIsCharacterGettingIn()
	{
		return m_bIsCharacterGettingIn;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns whether character is getting out
	bool GetIsCharacterGettingOut()
	{
		return m_bIsCharacterGettingOut;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns whether character is sprinting
	bool GetIsCharacterSprinting()
	{
		return m_bIsCharacterSprinting;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns whether character is swimming or not
	bool GetIsCharacterSwimming()
	{
		return m_bIsCharacterSwimming;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns whether character is falling (airborne) or not
	bool GetIsCharacterFalling()
	{
		return m_bIsCharacterFalling;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns whether character is reloading or not
	bool GetIsCharacterReloading()
	{
		return m_bIsCharacterReloading;
	}

	//------------------------------------------------------------------------------------------------
	//! Returns whether character is aiming down sights or not
	bool GetIsCharacterADS()
	{
		return m_bIsCharacterADS;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns if character has raised weapon
	bool GetIsCharacterWeaponRaised()
	{
		return m_bIsCharacterWeaponRaised;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Return if player can fire weapon 
	bool GetCanCharacterFireWeapon() { return m_bCanCharacterFireWeapon; }
	
	//------------------------------------------------------------------------------------------------
	//! Returns if character is currently using an item
	bool GetIsCharacterUsingItem()
	{
		return m_bIsCharacterUsingItem;
	}
	
	
	//------------------------------------------------------------------------------------------------
	//! Returns whether character can get out of vehicle (if in any)
	bool GetCanCharacterGetOutVehicle()
	{
		return m_bCanCharacterGetOutVehicle;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns if character is bleeding 
	bool GetIsCharacterBleeding()
	{
		return m_bIsCharacterBleeding;
	}
	
	//------------------------------------------------------------------------------------------------
	bool GetIsCharacterUsingRadio()
	{
		return m_bCharacterIsUsingRadio;
	}
	
	//------------------------------------------------------------------------------------------------
	int GetCharacterRadiosCount()
	{
		return m_bCharacterRadiosCount;
	}

	//------------------------------------------------------------------------------------------------
	bool IsInventoryOpen()
	{
		return m_bIsInventoryOpen;
	}

	//------------------------------------------------------------------------------------------------
	bool IsQuickSlotAvailable()
	{
		return m_bIsQuickSlotAvailable;
	}

	//------------------------------------------------------------------------------------------------
	bool IsQuickSlotShown()
	{
		return m_bIsQuickSlotShown;
	}
	
	//------------------------------------------------------------------------------------------------
	bool IsGadgetSelection()
	{
		return m_bIsGadgetSelection;
	}

	//------------------------------------------------------------------------------------------------
	//! Returns current character item or null if none
	IEntity GetCurrentItemEntity()
	{
		return m_pCurrentItemEntity;
	}

	//------------------------------------------------------------------------------------------------
	//! Returns current character weapon or null if none
	IEntity GetCurrentWeaponEntity()
	{
		return m_pCurrentWeaponEntity;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns current controlled vehicle
	IEntity GetCurrentVehicle()
	{
		return m_pCurrentVehicle;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns current controlled vehicle's controller
	BaseControllerComponent GetCurrentVehicleController()
	{
		return m_pCurrentVehicleController;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns current character weapon or null if none
	SignalsManagerComponent GetCurrentVehicleSignals()
	{
		return m_pCurrentVehicleSignals;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Returns current controlled vehicle weapon or null if none
	BaseWeaponComponent GetCurrentVehicleWeapon()
	{
		return m_pCurrentVehicleWeapon;
	}
	
	//------------------------------------------------------------------------------------------------	
	//! Returns for how long is vehicle using turbo
	float GetCurrentVehicleTurboTime()
	{
		return m_fTurboTime;
	}
	
	//------------------------------------------------------------------------------------------------	
	//! Returns the actual animation controller
	CharacterAnimationComponent GetAnimationComponent()
	{
		if (m_CharacterEntity)
			return m_CharacterEntity.GetAnimationComponent();
		
		return null;
	}
	
	
	//------------------------------------------------------------------------------------------------
	bool IsValid()
	{
		return m_bIsValid;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Fetches data from the provided entity
	//! Sets the validity of the data which can be received via IsValid()
	void FetchData(IEntity controlledEntity, float timeSlice)
	{	
		if (!controlledEntity)
		{
			m_bIsValid = false;
			return;
		}
		
		//if(!m_CharacterEntity)
		m_CharacterEntity = ChimeraCharacter.Cast(controlledEntity);
		if (!m_CharacterEntity)
		{
			m_bIsValid = false;
			return;
		}
		
		//if (!m_ControllerComponent)
		m_ControllerComponent = m_CharacterEntity.GetCharacterController();
		if (!m_ControllerComponent)
		{
			m_bIsValid = false;
			return;
		}
		
		//if(!m_CharacterInputConxtext)
		m_CharacterInputConxtext = m_ControllerComponent.GetInputContext();
		if (!m_CharacterInputConxtext)
		{
			m_bIsValid = false;
			return;
		}
		
		// Add inventory listeners for efficient magazine/grenade count updates
		if(!m_bIsSetted)
		{
			SCR_InventoryStorageManagerComponent inventory = SCR_InventoryStorageManagerComponent.Cast(m_CharacterEntity.FindComponent(SCR_InventoryStorageManagerComponent));
			inventory.m_OnItemAddedInvoker.Insert(OnItemAddedListener);
			inventory.m_OnItemRemovedInvoker.Insert(OnItemRemovedListener);
			inventory.m_OnInventoryOpenInvoker.Insert(OnInventoryOpen);
			inventory.m_OnQuickBarOpenInvoker.Insert(OnQuickSlotOpen);
			m_bIsSetted = true;
		}
		
		// Invalidates and clears any data prior to following collection
		Clear();
		
		// Current character stance
		m_eCharacterStance = m_ControllerComponent.GetStance();
		// Is character ADS?
		m_bIsCharacterADS = m_ControllerComponent.IsWeaponADS();
		// Is character sprinting?
		m_bIsCharacterSprinting = m_ControllerComponent.IsSprinting();
		// Is character swimming?
		m_bIsCharacterSwimming = m_ControllerComponent.IsSwimming();
		// Is character falling? (is airborne?)
		m_bIsCharacterFalling = m_ControllerComponent.IsFalling();
		// Is character in vehicle?
		m_bIsCharacterInVehicle = m_CharacterEntity.IsInVehicle();
		// Can character get out?
		m_bCanCharacterGetOutVehicle = m_ControllerComponent.CanGetOutVehicleScript();
		// Is character weapon raised?
		m_bIsCharacterWeaponRaised = m_CharacterInputConxtext.WeaponIsRaised();
		// Can character fire weapon 
		m_bCanCharacterFireWeapon = m_ControllerComponent.CanFire();
		// Is character currently using an item?
		m_bIsCharacterUsingItem = m_ControllerComponent.IsUsingItem();
		// Item that character is holding in his right hand
		m_pCurrentItemEntity = m_ControllerComponent.GetAttachedGadgetAtLeftHandSlot();
				
		// Temporary sprinting time tracking 
		if (m_bIsCharacterSprinting && !m_ControllerComponent.GetIsSprintingToggle())
			m_fSprintingTime += timeSlice;
		else 
			m_fSprintingTime = 0;

		// Vehicle compartment and controls
		CompartmentAccessComponent compartmentAccess = m_CharacterEntity.GetCompartmentAccessComponent();
		
		if (compartmentAccess)
		{
			m_bCanCharacterGetOutVehicle = m_bCanCharacterGetOutVehicle && compartmentAccess.CanGetOutVehicle();
			m_bIsCharacterGettingIn = compartmentAccess.IsGettingIn();
			m_bIsCharacterGettingOut = compartmentAccess.IsGettingOut();
			
			// Vehicle compartment
			BaseCompartmentSlot slot = compartmentAccess.GetCompartment();
			
			if (slot)
			{
				m_eCompartmentType = SCR_CompartmentAccessComponent.GetCompartmentType(slot);
				
				// Vehicle
				m_pCurrentVehicle = slot.GetOwner();
				
				if (m_pCurrentVehicle)
				{
					m_pCurrentVehicleController = slot.GetController();
					m_pCurrentVehicleSignals = SignalsManagerComponent.Cast(m_pCurrentVehicle.FindComponent(SignalsManagerComponent));
					
					// Temporary turbo time tracking
					if (m_eCompartmentType == ECompartmentType.Pilot && GetGame().GetInputManager().GetActionTriggered("CarTurbo"))
						m_fTurboTime += timeSlice;
					else 
						m_fTurboTime = 0;
				}
				
				// Turret controls 
				TurretControllerComponent turretController = TurretControllerComponent.Cast(slot.GetController());
				
				if(turretController)
				{
					m_pCurrentVehicleWeapon = turretController.GetWeaponManager().GetCurrentWeapon();
				}
			}
		}
		
		// Current character weapon manager
		// Current character weapon
		BaseWeaponManagerComponent weaponManagerComponent = m_ControllerComponent.GetWeaponManagerComponent();
		if (weaponManagerComponent)
		{
			// Weapon slot -> weapon entity
			WeaponSlotComponent currentSlot = weaponManagerComponent.GetCurrentSlot();
			if (currentSlot)
				m_pCurrentWeaponEntity = currentSlot.GetWeaponEntity();
			
			// BaseWeaponComponent
			BaseWeaponComponent currentWeapon = weaponManagerComponent.GetCurrent();
			
			if (currentWeapon != m_pCurrentWeapon)
			{
				m_pCurrentWeapon = currentWeapon;
				m_bCanFetchInventory = true;
			}
			
			// Muzzle and magazine
			if (m_pCurrentWeapon)
			{
				m_pCurrentMuzzle = m_pCurrentWeapon.GetCurrentMuzzle();
				m_pCurrentMagazine = m_pCurrentWeapon.GetCurrentMagazine();
			}
			
			// Is character reloading 
			m_bIsCharacterReloading = m_ControllerComponent.IsReloading();
		}
		
		// Does character have additional mags for current weapon?
		if(m_bCanFetchInventory)
		{
			SCR_InventoryStorageManagerComponent inventoryMgr = SCR_InventoryStorageManagerComponent.Cast(m_CharacterEntity.FindComponent(SCR_InventoryStorageManagerComponent));
			SCR_CharacterInventoryStorageComponent characterStorage = SCR_CharacterInventoryStorageComponent.Cast(m_CharacterEntity.FindComponent(SCR_CharacterInventoryStorageComponent));
			
			if (inventoryMgr)
			{
				if (m_pCurrentWeapon)
					m_iAdditionalMagazines = inventoryMgr.GetMagazineCountByWeapon(m_pCurrentWeapon);
				
				m_iMedicalItemCount = inventoryMgr.GetHealthComponentCount();
			}				
			
			// Check medical items in quick slots 
			if (characterStorage)
			{
				m_iMedicalItemCountInQuickSlots = 0;
				
				array<IEntity> items = characterStorage.GetQuickSlotItems();
				foreach (IEntity item : items)
				{
					if (item && SCR_ConsumableItemComponent.Cast(item.FindComponent(SCR_ConsumableItemComponent)))
						m_iMedicalItemCountInQuickSlots++;
				}
			}
			
			m_bCanFetchInventory = false;
		}
		
		// Camera handler to check focus mode
		CameraHandlerComponent cameraHandler = CameraHandlerComponent.Cast(m_CharacterEntity.FindComponent(CameraHandlerComponent));
		if (cameraHandler)
		{
			m_fFocusMode = cameraHandler.GetFocusMode();
		}
		
		// Check bleeding 
		DamageManagerComponent damageManager = m_CharacterEntity.GetDamageManager();
		
		if (damageManager)
		{
			m_bIsCharacterBleeding = damageManager.IsDamagedOverTime(EDamageType.BLEEDING);
		}
		
		// Bleeding time tracking 
		if (m_bIsCharacterBleeding)
		{
			m_fBleedingTime += timeSlice;
		}
		else 
		{
			m_fBleedingTime = 0;
		}
		
		// Fetch available gadgets
		GetAvailableGadgets();
		
		VonActions();
		
		return;
	}
	
	//------------------------------------------------------------------------------------------------
	//! Check character von actions 
	protected void VonActions()
	{
		if (!m_CharacterEntity)
			return;
		
		if (!m_ControllerComponent)
			return;
		
		SCR_VONController vonController = SCR_VONController.Cast(GetGame().GetPlayerController().FindComponent(SCR_VONController));
		if (!vonController)
			return;
		
		m_bCharacterRadiosCount = vonController.GetVONEntries().Count();
		
		// Find von component 
		SCR_VoNComponent von = SCR_VoNComponent.Cast(m_CharacterEntity.FindComponent(SCR_VoNComponent));
		if (!von)
			return;
		
		m_bCharacterIsUsingRadio = von.IsTransmiting();
	}
	
	//------------------------------------------------------------------------------------------------
	//! Creates condition data container
	void SCR_AvailableActionsConditionData()
	{
	}
};