#ifdef BREAK_COMPILATION
	THIS DEFINE BREAKS GAME SCRIPT MODULE COMPILATION
	DO NOT REMOVE IT, IT IS NEEDED FOR VARIOUS TESTS
#endif

/*! \mainpage Main Page

Welcome to Arma Reforger script documentation.
*/

//------------------------------------------------------------------------------------------------
//! GameMode Game Flags represented by bit mask
enum EGameFlags
{
	Metabolism = 1,
	SpawnVehicles = 2,
	SpawnAI = 4,
	Last = 4
};

//------------------------------------------------------------------------------------------------
//! Enum of DiagMenu id values, generated by Game
enum EDiagMenuGame : EDiagMenuGameLib
{
};

//------------------------------------------------------------------------------------------------
/*!
Main game instance.
Created when the game starts (not when a world starts!) and persists until the game is closed.
*/
class ArmaReforgerScripted : ChimeraGame
{
	const string CONFIG_CORES_PATH = "Configs/Core/";

	const ResourceName CONFIG_DIALOGS_ERROR = "{D3BFEE28E7D5B6A1}Configs/ServerBrowser/KickDialogs.conf";

	//! Game Flags specific for current game mode, or for the game mode which is going to be played when it is a host/server side.
	protected EGameFlags 					m_eGameFlags;
	protected bool							m_bAreGameFlagsObtained;
	protected ref ScriptInvoker				Event_OnObtainedGameFlags = new ScriptInvoker;
	protected SCR_HUDManagerComponent		m_HUDManager;
	protected ScriptedChatEntity			m_ChatEntity;
	protected ref SCR_GameCoresManager		m_CoresManager;
	protected Widget 						m_wWatermark;


	//! Object responsible for managing and providing game modes with list of available loadouts.
	protected SCR_LoadoutManager m_pLoadoutManager;

	ref ScriptInvoker m_OnMissionSetInvoker = new ScriptInvoker();
	ref RplSessionErrorHandler m_SessionErrorHandler;

	bool m_bIsMainMenuOpen = false;
	private bool m_bGameStarted = false;

	//! Stack of queued error messages. All messages are popped one by one as user closes them and only in the main menu
	protected ref SCR_Stack<SCR_GameErrorMessage> m_aErrorStack = new ref SCR_Stack<SCR_GameErrorMessage>();

	//------------------------------------------------------------------------------------------------
	protected ref ScriptInvoker m_OnChangeUserSettingsInvoker = new ScriptInvoker();
	protected ref ScriptInvoker m_OnInputDeviceUserChangedInvoker = new ScriptInvoker();
	protected ref ScriptInvoker m_OnWorldSimulatePhysicsInvoker = new ScriptInvoker();
	protected ref ScriptCallQueue m_Callqueue = new ScriptCallQueue();

	//------------------------------------------------------------------------------------------------
	void ~ArmaReforgerScripted()
	{
		g_ARGame = null;
	}

	//------------------------------------------------------------------------------------------------
	/*!
		Returns currently registered SCR_LoadoutManager if any is present.
		\return Reference to loadout manager or null if none.
	*/
	SCR_LoadoutManager GetLoadoutManager()
	{
		return m_pLoadoutManager;
	}

	//------------------------------------------------------------------------------------------------
	ScriptCallQueue GetCallqueue()
	{
		return m_Callqueue;
	}

	//------------------------------------------------------------------------------------------------
	ScriptInvoker OnUserSettingsChangedInvoker()
	{
		return m_OnChangeUserSettingsInvoker;
	}

	//------------------------------------------------------------------------------------------------
	ScriptInvoker OnInputDeviceUserChangedInvoker()
	{
		return m_OnInputDeviceUserChangedInvoker;
	}

	//------------------------------------------------------------------------------------------------
	ScriptInvoker OnWorldSimulatePhysicsInvoker()
	{
		return m_OnWorldSimulatePhysicsInvoker;
	}

	//------------------------------------------------------------------------------------------------
	override protected void OnMissionSet(MissionHeader mission)
	{
		m_OnMissionSetInvoker.Invoke(mission);
		Print("OnMissionSet");

		SCR_MissionHeader pHeader = SCR_MissionHeader.Cast(mission);
		if (pHeader)
			SetGameFlags(pHeader.m_eDefaultGameFlags, false);
	}

	//------------------------------------------------------------------------------------------------
	override protected void OnKickedFromGame(KickCauseCode kickCode)
	{
		KickCauseGroup2 groupInt = KickCauseCodeAPI.GetGroup(kickCode);
		int reasonInt = KickCauseCodeAPI.GetReason(kickCode);
		string group = "<unknown>";
		string reason = "<unknown>";
		switch(groupInt)
		{
			case KickCauseGroup.REPLICATION:
				group = "REPLICATION";
				switch(reasonInt)
				{
					case RplError.SYSTEM_FAILURE: reason = "SYSTEM_FAILURE"; break;
					case RplError.DISCONNECTION: reason = "DISCONNECTION"; break;
					case RplError.CONNECTION_FAILURE: reason = "CONNECTION_FAILURE"; break;
					case RplError.TIMEOUT: reason = "TIMEOUT"; break;
					case RplError.SERVICE_FAILURE: reason = "SERVICE_FAILURE"; break;
				}
			break;

			case KickCauseGroup.BATTLEYE_INIT:
				group = "BATTLEYE_INIT";
				switch(reasonInt)
				{
					case BattlEyeInitError.LOAD_ERROR: reason = "LOAD_ERROR"; break;
					case BattlEyeInitError.UNSUPPORTED_VERSION: reason = "UNSUPPORTED_VERSION"; break;
					case BattlEyeInitError.OTHER_ERROR: reason = "OTHER_ERROR"; break;
				}
			break;

			case KickCauseGroup.BATTLEYE:
				group = "BATTLEYE";
			break;

			case KickCauseGroup.DATA:
				group = "DATA";
				switch(reasonInt)
				{
					case DataError.VERSION_MISMATCH: reason = "VERSION_MISMATCH"; break;
					case DataError.RDB_MISMATCH: reason = "RDB_MISMATCH"; break;
					case DataError.SCRIPT_MISMATCH: reason = "SCRIPT_MISMATCH"; break;
					case DataError.WORLD_LOAD_ERROR: reason = "WORLD_LOAD_ERROR"; break;
					case DataError.WORLD_LOAD_INCONSISTENCY: reason = "WORLD_LOAD_INCONSISTENCY"; break;
				}
			break;

			case KickCauseGroup2.PLATFORM:
				group = "PLATFORM";
				switch(reasonInt)
				{
					case PlatformKickReason.ACTIVE_USER_LOST: reason = "ACTIVE_USER_LOST"; break;
					case PlatformKickReason.NO_MP_PRIVILEGE: reason = "NO_MP_PRIVILEGE"; break;
				}
			break;

			case KickCauseGroup2.PLAYER_MANAGER:
				group = "PLAYER_MANAGER";
				switch (reasonInt)
				{
					//FIXME: KICK_REQUESTED -> KICK
					case PlayerManagerKickReason.KICK: reason = "KICK_REQUESTED"; break;
					//FIXME: KICK_VOTED
					case PlayerManagerKickReason.KICK_VOTED: reason = "KICK_VOTED"; break;
					case PlayerManagerKickReason.DUPLICATE_PLAYER_IDENTITY: reason = "DUPLICATE_PLAYER_IDENTITY"; break;
					case SCR_PlayerManagerKickReason.KICKED_BY_GM: reason = "KICKED_BY_GM"; break;
					case SCR_PlayerManagerKickReason.FRIENDLY_FIRE: reason = "FRIENDLY_FIRE"; break;
					case SCR_PlayerManagerKickReason.DISRUPTIVE_BEHAVIOUR: reason = "DISRUPTIVE_BEHAVIOUR"; break;
				}
			break;
		}

		// Detail
		string format = "Kick cause code: category=%1 '%2', reason=%3 '%4'";
		string strDetail = string.Format(format , groupInt, group, reasonInt, reason);

		//  Set dialog tag
		string dialogTag = group + "_" + reason;

		// Default error
		if (group == "<unknown>")
			dialogTag = "DEFAULT_ERROR";

		// Set msg
		ServerBrowserMenuUI.SetErrorMessage(dialogTag, strDetail);
	}

	//------------------------------------------------------------------------------------------------
	//! Event called once loading of all entities of the world have been finished. (still within the loading)
	protected override void OnWorldPostProcess(World world)
	{
		if (GetGameMode())
			GetGameMode().OnWorldPostProcess(world);
	}

	//------------------------------------------------------------------------------------------------
	//! Push an error message to the error message stack
	protected override void ShowErrorMessage(string msg)
	{
		ref auto errorMessage = new ref SCR_GameErrorMessage(msg, "Error");
		m_aErrorStack.Push(errorMessage);
 	}

	//------------------------------------------------------------------------------------------------
	//! If no error dialogue is currently shown try to pop it
	//! from the error stack and display it as an error dialogue
	protected void ShowNextErrorDialog()
	{
		MenuManager menuManager = GetMenuManager();
		if (!menuManager)	// No menu manager, we have no means of displaying the error atm
			return;

		DialogUI currentDialog = DialogUI.Cast(menuManager.FindMenuByPreset(ChimeraMenuPreset.ErrorDialog));
		if (currentDialog != null)	// There is already an error dialog displayed, do not display next one
			return;

		auto errorMessage = m_aErrorStack.Pop();
		if (!errorMessage)
			return;	// Should not occur as we're checking for whether the stack is empty

		// Display the error message using DialogUI menu dialogue
		if (errorMessage)
		{
			DialogUI dialog = DialogUI.Cast(menuManager.OpenDialog(ChimeraMenuPreset.ErrorDialog));
			if (dialog)
			{
				dialog.SetTitle(errorMessage.GetTitle());
				dialog.SetMessage(errorMessage.GetMessage());
			}
		}
	}

	//------------------------------------------------------------------------------------------------
	bool GetGameStarted()
	{
		return m_bGameStarted;
	}

	//------------------------------------------------------------------------------------------------
	EGameFlags GetGameFlags()
	{
		return m_eGameFlags;
	}

	//------------------------------------------------------------------------------------------------
	void SetGameFlags(EGameFlags newGameFlags, bool shouldInvoke)
	{
		m_bAreGameFlagsObtained = true;
		m_eGameFlags 			= newGameFlags;

		if (shouldInvoke)
		{
			InvokeGameFlags();
		}
	}

	//------------------------------------------------------------------------------------------------
	//! You should register to Event_OnObtainedGameFlags, but for cases you can't use this.
	bool AreGameFlagsObtained()
	{
		return m_bAreGameFlagsObtained;
	}

	//------------------------------------------------------------------------------------------------
	//! Can check for multiple bits at the same time.
	bool AreGameFlagsSet(EGameFlags checkGameFlags)
	{
		return (m_eGameFlags & checkGameFlags) != 0;
	}

	//------------------------------------------------------------------------------------------------
	ScriptInvoker GetOnObtainedGameFlagsInvoker()
	{
		return Event_OnObtainedGameFlags;
	}

	//------------------------------------------------------------------------------------------------
	protected void InvokeGameFlags()
	{
		Event_OnObtainedGameFlags.Invoke();
	}

	//------------------------------------------------------------------------------------------------
	//! Important for menu manager to work. Specify presets in ChimeraMenuPreset enum and add them in chimeraMenus.conf configuration file.
	override typename GetMenuPreset()
	{
		return ChimeraMenuPreset;
	}

	//------------------------------------------------------------------------------------------------
	override LoadingAnim CreateLoadingAnim(WorkspaceWidget workspaceWidget)
	{
		return new ArmaReforgerLoadingAnim(workspaceWidget);
	}

	//------------------------------------------------------------------------------------------------
	override void OnAfterInit(BaseWorld world)
	{
		m_CoresManager = SCR_GameCoresManager.CreateCoresManager();
		WidgetManager.SetCursor(0);

		//required for DS registration with -dserver param
		if (System.IsConsoleApp())
		{
			string sProfileName = SCR_Global.GetProfileName();
		}
	}

	//------------------------------------------------------------------------------------------------
	override bool OnGameStart()
	{
		m_bGameStarted = true;

		DiagMenu.RegisterItem(SCR_DebugMenuID.DEBUGUI_INPUT_MANAGER, "", "Show input manager", "GameCode", "disabled,active,all");

		#ifdef ENABLE_DIAG
		// Game
		DiagMenu.RegisterBool(SCR_DebugMenuID.DEBUGUI_COPY_ENF_VIEW_LINK, "lctrl+lshift+l", "Copy view link", "Game");
		DiagMenu.RegisterBool(SCR_DebugMenuID.DEBUGUI_CURSOR_TARGET_PREFAB_DIAG, "", "Show cursor target info", "Game");
		DiagMenu.RegisterBool(SCR_DebugMenuID.DEBUGUI_BOUNDS_OVERLAP_PREFAB_DIAG, "", "Show bounds overlap target info", "Game");

		// UI
		DiagMenu.RegisterBool(SCR_DebugMenuID.DEBUGUI_UI_CLOSE_ALL_MENUS, "", "Close All Menus", "UI");
		DiagMenu.RegisterBool(SCR_DebugMenuID.DEBUGUI_UI_OPEN_MAIN_MENU, "", "Open Main Menu", "UI");
		DiagMenu.RegisterBool(SCR_DebugMenuID.DEBUGUI_UI_LOG_UNDER_CURSOR, "", "Log widgets under cursor", "UI");
		#endif

		if (!GetWorldEntity() || RplSession.Mode() != RplMode.Client)
		{
			m_bAreGameFlagsObtained = true;
			InvokeGameFlags();
		}

		InputManager inputManager = GetInputManager();
		inputManager.AddActionListener("ShowScoreboard", EActionTrigger.DOWN, OnShowPlayerList);
		inputManager.AddActionListener("MenuOpen", EActionTrigger.DOWN, OnMenuOpen);
		#ifdef WORKBENCH
			inputManager.AddActionListener("MenuOpenWB", EActionTrigger.DOWN, OnMenuOpen);
		#endif

		if (m_CoresManager)
			m_CoresManager.OnGameStart();

		m_SessionErrorHandler = new RplSessionErrorHandler();

		// Init early access watermark:
		m_wWatermark = GetGame().GetWorkspace().CreateWidgets("{B7A765172F0BD4D9}UI/layouts/Common/EarlyAccessWatermark.layout", GetGame().GetWorkspace());

#ifndef PLATFORM_CONSOLE
		if (System.IsCLIParam("listScenarios"))
			SCR_GameLogHelper.LogScenariosConfPaths();
#endif

		return true;
	}

	//------------------------------------------------------------------------------------------------
	protected static void OnMenuOpen()
	{
		MenuManager menuManager = GetGame().GetMenuManager();
		if (!menuManager || menuManager.IsAnyMenuOpen() || menuManager.IsAnyDialogOpen())
			return;

		OpenPauseMenu();
	}

	//------------------------------------------------------------------------------------------------
	static void OnShowPlayerList()
	{
		MenuManager manager = GetGame().GetMenuManager();
		if (manager.IsAnyMenuOpen() || manager.IsAnyDialogOpen())
			return;

		OpenPlayerList();
	}

	//------------------------------------------------------------------------------------------------
	static void OpenPauseMenu(bool hideParentMenu = true, bool fadeBackground = false)
	{
		MenuBase menu = GetGame().GetMenuManager().OpenMenu(ChimeraMenuPreset.PauseMenu, 0, true, hideParentMenu);
		if (!fadeBackground)
			return;

		PauseMenuUI pauseMenu = PauseMenuUI.Cast(menu);
		if (pauseMenu)
			pauseMenu.FadeBackground(true, true);
	}

	//------------------------------------------------------------------------------------------------
	static SCR_PlayerListMenu OpenPlayerList()
	{
		MenuManager menuManager = GetGame().GetMenuManager();
		if (menuManager.IsAnyDialogOpen())
			return null; // We don't want to open this menu behind any dialogs.

		MenuBase menu = menuManager.FindMenuByPreset(ChimeraMenuPreset.PlayerListMenu);
		if (!menu)
			menu = menuManager.OpenMenu(ChimeraMenuPreset.PlayerListMenu);

		return SCR_PlayerListMenu.Cast(menu);
	}

	//------------------------------------------------------------------------------------------------
	override void OnGameEnd()
	{
		m_bAreGameFlagsObtained = false;
		Event_OnObtainedGameFlags.Clear();
		GetGame().GetMenuManager().CloseAllMenus();
		if (m_wWatermark)
			m_wWatermark.RemoveFromHierarchy();

		SCR_BaseGameMode gameMode = SCR_BaseGameMode.Cast(GetGameMode());
		if (gameMode)
			gameMode.OnGameEnd();

		ShutdownBackend();

		if (m_CoresManager)
			m_CoresManager.OnGameEnd();
	}

	//------------------------------------------------------------------------------------------------
	override void OnUserSettingsChangedEvent()
	{
		//Print("OnUserSettingsChangedEvent");
		m_OnChangeUserSettingsInvoker.Invoke();
	}

	//------------------------------------------------------------------------------------------------
	override void OnInputDeviceUserChangedEvent(EInputDeviceType oldDevice, EInputDeviceType newDevice)
	{
		m_OnInputDeviceUserChangedInvoker.Invoke(oldDevice, newDevice);
	}

	//------------------------------------------------------------------------------------------------
	override void OnWorldSimulatePhysics(float timeSlice)
	{
		m_OnWorldSimulatePhysicsInvoker.Invoke(timeSlice);
	}

	#ifdef ENABLE_DIAG
	private ref QueryTargetDiag m_pQueryTargetDiag = new QueryTargetDiag();
	#endif

	//------------------------------------------------------------------------------------------------
	override void OnUpdate(BaseWorld world, float timeslice)
	{
		super.OnUpdate(world, timeslice);

		m_Callqueue.Tick(timeslice);

		// Setup in-game context
		if (!GetMenuManager().IsAnyMenuOpen())
		{
			GetInputManager().ActivateContext("IngameContext", 1);
		}

		// If we're in the main menu and there are errors on the error stack
		// process them one by one
		if (m_bIsMainMenuOpen)
		{
			if (!m_aErrorStack.IsEmpty())
			{
				ShowNextErrorDialog();
			}
		}

		GetInputManager().SetDebug(DiagMenu.GetValue(SCR_DebugMenuID.DEBUGUI_INPUT_MANAGER));

		// Check clipboard link diag
		#ifdef ENABLE_DIAG
		bool bGetEnfLinkToClipboard = DiagMenu.GetBool(SCR_DebugMenuID.DEBUGUI_COPY_ENF_VIEW_LINK);
		if (bGetEnfLinkToClipboard)
		{
			// Out to clip
			vector cameraTransform[4];
			world.GetCurrentCamera(cameraTransform);
			System.ExportToClipboard(GetWorldEditorLink(cameraTransform));
			// Reset
			DiagMenu.SetValue(SCR_DebugMenuID.DEBUGUI_COPY_ENF_VIEW_LINK, 0);
		}

		if (DiagMenu.GetBool(SCR_DebugMenuID.DEBUGUI_CURSOR_TARGET_PREFAB_DIAG))
		{
			if (GetGame().GetCameraManager())
			{
				CameraBase current = GetGame().GetCameraManager().CurrentCamera();
				if (current)
				{
					DbgUI.Begin("Cursor target info");
					IEntity ent = current.GetCursorTarget();
					if (ent)
					{
						// Name
						string name = ent.GetName();
						// Prefab data
						EntityPrefabData prefabData = ent.GetPrefabData();
						string prefabName = "";
						if (prefabData)
							prefabName = prefabData.GetPrefabName();
						// Draw text
						DbgUI.Text("Name: " + name);
						DbgUI.Text("Prefab: " + prefabName);
						DbgUI.Spacer(32);

						string infoText = string.Format("Name=\"%1\"\nPrefab=\"%2\"", name, prefabName);
						if (DbgUI.Button("Copy to clipboard"))
						{

							System.ExportToClipboard(infoText);
						}

						// Draw ddbox
						vector mins, maxs;
						ent.GetWorldBounds(mins, maxs);
						ref Shape boundingFill = Shape.Create(ShapeType.BBOX, ARGB(5, 0, 255, 0), ShapeFlags.ONCE | ShapeFlags.NOZBUFFER | ShapeFlags.TRANSP, mins, maxs);
						ref Shape boundingWire = Shape.Create(ShapeType.BBOX, ARGB(200, 0, 255, 255), ShapeFlags.ONCE | ShapeFlags.NOZBUFFER | ShapeFlags.WIREFRAME | ShapeFlags.TRANSP, mins, maxs);

						vector pos = ent.GetOrigin();
						DebugTextWorldSpace.Create(current.GetWorld(), infoText, DebugTextFlags.FACE_CAMERA | DebugTextFlags.CENTER | DebugTextFlags.ONCE, pos[0], maxs[1] + 0.5, pos[2], 8, ARGB(255, 0, 255, 255), ARGB(64, 0, 0, 0));
					}
					DbgUI.End();
				}
			}
		}

		if (DiagMenu.GetBool(SCR_DebugMenuID.DEBUGUI_BOUNDS_OVERLAP_PREFAB_DIAG))
		{
			if (GetGame().GetCameraManager())
			{
				CameraBase current = GetGame().GetCameraManager().CurrentCamera();
				if (current)
				{
					DbgUI.Begin("Bounds overlap target info");
					vector cameraMat[4];
					current.GetWorldTransform(cameraMat);

					m_pQueryTargetDiag.Prepare();
					m_pQueryTargetDiag.DoQuery(current.GetWorld(), cameraMat[3], cameraMat[3] + 3.0 * cameraMat[2]);
					IEntity ent = m_pQueryTargetDiag.GetClosestEntity(cameraMat[3]);
					if (ent)
					{
						// Name
						string name = ent.GetName();
						// Prefab data
						EntityPrefabData prefabData = ent.GetPrefabData();
						string prefabName = "";
						if (prefabData)
							prefabName = prefabData.GetPrefabName();
						// Draw text
						DbgUI.Text("Name: " + name);
						DbgUI.Text("Prefab: " + prefabName);
						DbgUI.Spacer(32);

						string infoText = string.Format("Name=\"%1\"\nPrefab=\"%2\"", name, prefabName);
						if (DbgUI.Button("Copy to clipboard"))
						{

							System.ExportToClipboard(infoText);
						}

						// Draw ddbox
						vector mins, maxs;
						ent.GetWorldBounds(mins, maxs);
						ref Shape boundingFill = Shape.Create(ShapeType.BBOX, ARGB(5, 0, 255, 0), ShapeFlags.ONCE | ShapeFlags.NOZBUFFER | ShapeFlags.TRANSP, mins, maxs);
						ref Shape boundingWire = Shape.Create(ShapeType.BBOX, ARGB(200, 0, 255, 255), ShapeFlags.ONCE | ShapeFlags.NOZBUFFER | ShapeFlags.WIREFRAME | ShapeFlags.TRANSP, mins, maxs);

						vector pos = ent.GetOrigin();
						DebugTextWorldSpace.Create(current.GetWorld(), infoText, DebugTextFlags.FACE_CAMERA | DebugTextFlags.CENTER | DebugTextFlags.ONCE, pos[0], maxs[1] + 0.5, pos[2], 8, ARGB(255, 0, 255, 255), ARGB(64, 0, 0, 0));
					}
					DbgUI.End();
				}
			}
		}

		// Close all menus
		if (DiagMenu.GetBool(SCR_DebugMenuID.DEBUGUI_UI_CLOSE_ALL_MENUS))
		{
			DiagMenu.SetValue(SCR_DebugMenuID.DEBUGUI_UI_CLOSE_ALL_MENUS, 0);
			GetGame().GetMenuManager().CloseAllMenus();
		}

		// Open main menu
		if (DiagMenu.GetBool(SCR_DebugMenuID.DEBUGUI_UI_OPEN_MAIN_MENU))
		{
			DiagMenu.SetValue(SCR_DebugMenuID.DEBUGUI_UI_OPEN_MAIN_MENU, 0);
			GetGame().GetMenuManager().OpenMenu(ChimeraMenuPreset.MainMenu);
		}

		if (DiagMenu.GetBool(SCR_DebugMenuID.DEBUGUI_UI_LOG_UNDER_CURSOR))
		{
			DiagMenu.SetValue(SCR_DebugMenuID.DEBUGUI_UI_LOG_UNDER_CURSOR, 0);

			array<ref Widget> widgets = {};
			int mouseX, mouseY;
			WidgetManager.GetMousePos(mouseX, mouseY);
			WidgetManager.TraceWidgets(mouseX, mouseY, GetGame().GetWorkspace(), widgets);
			int count = widgets.Count();
			PrintFormat("Widgets under cursor (x = %1, y = %2): %3", mouseX, mouseY, count);
			for (int i; i < count; i++)
			{
				Print("  " + SCR_WidgetTools.GetHierarchyLog(widgets[i]));
			}
		}

		#endif

		if (m_CoresManager)
			m_CoresManager.OnUpdate(timeslice);
	}

	//------------------------------------------------------------------------------------------------
	void ShutdownBackend()
	{
//		BackendApi backendApi = GetBackendApi();
//		if (backendApi.IsAuthenticated())
//			backendApi.Shutdown();
	}

	//------------------------------------------------------------------------------------------------
	void SetHUDManager(SCR_HUDManagerComponent hud)
	{
		m_HUDManager = hud;
	}

	//------------------------------------------------------------------------------------------------
	/*!
		Registers provided loadout manager as the one used by the game.
		Multiple registrations are not supported and will log an error.
		\param instance Instance to register.
	*/
	void RegisterLoadoutManager(SCR_LoadoutManager instance)
	{
		if (m_pLoadoutManager)
		{
			Print("Trying to register a SCR_LoadoutManager, but one is already registered!", LogLevel.ERROR);
			return;
		}

		m_pLoadoutManager = instance;
	}

	//------------------------------------------------------------------------------------------------
	/*!
		Unregisters provided loadout manager.
		Wrong unregistrations will log an error.
		\param instance Instance to unregister.
	*/
	void UnregisterLoadoutManager(SCR_LoadoutManager instance)
	{
		if (!m_pLoadoutManager)
		{
			Print("Trying to unregister a SCR_LoadoutManager, but none is registered!", LogLevel.ERROR);
			return;
		}

		if (!instance)
		{
			Print("Trying to unregister an invalid SCR_LoadoutManager!", LogLevel.ERROR);
			return;
		}

		if (m_pLoadoutManager != instance)
		{
			Print("Trying to unregister a SCR_LoadoutManager, but different one is registered!", LogLevel.ERROR);
			return;
		}

		Print("SCR_LoadoutManager unregistered successfully, released instance: " + instance, LogLevel.VERBOSE);
		// Release reference
		m_pLoadoutManager = null;
	}

	//------------------------------------------------------------------------------------------------
	SCR_HUDManagerComponent GetHUDManager()
	{
		return m_HUDManager;
	}

	//------------------------------------------------------------------------------------------------
	#ifdef ENABLE_DIAG
	/*!
		Return enfusion protocol link for world editor.
		\param transformation The transformation to create the link at.
	*/
	static string GetWorldEditorLink(vector transformation[4])
	{


		// Fetch position
		vector position = transformation[3];

		// Fetch angles
		vector angles = Math3D.MatrixToAngles(transformation);

		// We want to substring only /worlds/...
		// to prevent exposing local folders, etc.
		string fullLink = GetGame().GetWorldFile();
		string fullLinkLower = fullLink;
		fullLinkLower.ToLower(); // In case another casing of the folder is used
		int begin = fullLinkLower.IndexOf("worlds/");
		string worldPath = fullLink.Substring(begin, fullLink.Length() - begin);

		// Create link
		string link = string.Format(
		"enfusion://WorldEditor/%1;%2,%3,%4;%5,%6,%7",
		worldPath,
		position[0],
		position[1],
		position[2],
		angles[1],
		angles[0],
		angles[2]);

		// Print it to console
		return link;
	}
	#endif

	override string GetMissionName()
	{
		SCR_MissionHeader header = SCR_MissionHeader.Cast(GetMissionHeader());
		if (header)
			return header.m_sName;
		else
			return "";
	}

	//------------------------------------------------------------------------------------------------
	void PlayMission(SCR_MissionHeader header)
	{
		if (!header)
			return;
		Print(string.Format("Play mission: %1", header.m_sName));

		if (GameStateTransitions.RequestMissionChangeTransition(header))
			GetGame().GetMenuManager().CloseAllMenus();
		else
			Print(string.Format("Failed to start mission: %1", header.m_sName), LogLevel.ERROR);

		// TODO: Show invalid mission dialog
	}

	//------------------------------------------------------------------------------------------------
	override void PlayGameConfig(string sResource)
	{
		ref SCR_MissionHeader header = SCR_MissionHeader.Cast(SCR_MissionHeader.ReadMissionHeader(sResource));
		PlayMission(header);
	}


	//------------------------------------------------------------------------------------------------
	override void HostGameConfig(string sResource)
	{
		ref SCR_MissionHeader header = SCR_MissionHeader.Cast(SCR_MissionHeader.ReadMissionHeader(sResource));
		HostMission(header);
	}

	//------------------------------------------------------------------------------------------------
	void HostMission(SCR_MissionHeader header)
	{
		if (!header)
			return;

		bool success = GameStateTransitions.RequestPublicServerTransition(header, new ArmaReforgerServerParams(header));

		if (success)
			GetGame().GetMenuManager().CloseAllMenus();
		else
			Print(string.Format("Failed to host mission: %1", header.m_sName), LogLevel.ERROR);

		// TODO: Show invalid mission dialog
	}

	//------------------------------------------------------------------------------------------------
	//! Get a list of all missions, featured, tutorial, recommended ones
	//! \return mission resources list
	override array<ResourceName> GetDefaultGameConfigs()
	{
		ResourceName config = "{6409EA8EA4BFF7E6}Configs/PlayMenu/PlayMenuEntries.conf";
		Resource resource = BaseContainerTools.LoadContainer(config);
		if (!resource)
			return null;

		BaseContainer entries = resource.GetResource().ToBaseContainer();
		if (!entries)
			return null;

		ResourceName featured;
		ResourceName tutorial;
		array<ResourceName> recommended = {};

		entries.Get("m_FeaturedMission", featured);
		entries.Get("m_TutorialMission", tutorial);
		entries.Get("m_aRecommendedMissions", recommended);

		array<ResourceName> resources = {};
		resources.Insert(tutorial);
		resources.Insert(featured);

		foreach(ResourceName r : recommended)
		{
			if (r != tutorial && r != featured)
				resources.Insert(r);
		}
		return resources;
	}

	//------------------------------------------------------------------------------------------------
	protected static bool CheckMissionHeader(MissionWorkshopItem mission)
	{
		SCR_MissionHeader header = SCR_MissionHeader.Cast(mission.GetHeader());

		if (!header)
		{
			Print(string.Format("Mission header doesn't exist or is not of SCR_MissionHeader class: %1", mission.Name()), LogLevel.ERROR);
			return false;
		}

		string worldPath = header.GetWorldPath();
		if (worldPath.IsEmpty())
		{
			Print(string.Format("Mission world path is incorrect: %1", mission.Name()), LogLevel.ERROR);
			return false;
		}

		return true;
	}

	//------------------------------------------------------------------------------------------------
	/*
		Returns current local player picture in picture sights, if any.
	*/
	static SCR_2DPIPSightsComponent GetCurrentPIPSights()
	{
		IEntity controlledEntity = SCR_PlayerController.GetLocalControlledEntity();
		if (!controlledEntity)
			return null;

		ChimeraCharacter character = ChimeraCharacter.Cast(controlledEntity);
		if (!character)
			return null;

		BaseWeaponManagerComponent weaponManager = character.GetCharacterController().GetWeaponManagerComponent();
		if (!weaponManager)
			return null;

		BaseSightsComponent currentSights = weaponManager.GetCurrentSights();
		if (!currentSights)
			return null;

		SCR_2DPIPSightsComponent pip = SCR_2DPIPSightsComponent.Cast(currentSights);
		if (!pip)
			return null;

		return pip;
	}

	//------------------------------------------------------------------------------------------------
	/*!
		Returns whether screen position is in located within the sights radius.
		\param screenPosition Point on screen
		\param sightsComponent Picture in picture sights to use as reference
		\return Returns true if point is in picture in picture sights, false otherwise.
	*/
	static bool IsScreenPointInPIPSights(vector screenPosition, SCR_2DPIPSightsComponent sightsComponent)
	{
		if (!sightsComponent)
			return false;

		return sightsComponent.IsScreenPositionInSights(screenPosition);
	}
};

//------------------------------------------------------------------------------------------------
ArmaReforgerScripted g_ARGame;

Game CreateGame()
{
    g_ARGame = new ArmaReforgerScripted;
    return g_ARGame;
};

ArmaReforgerScripted GetGame()
{
	return g_ARGame;
};

#ifdef ENABLE_DIAG
class QueryTargetDiag
{
	private ref array<IEntity> m_aQueriedTargetEntities = {};

	// Prepares for collection
	void Prepare()
	{
		m_aQueriedTargetEntities.Clear();
	}

	// Collects nearby entities
	void DoQuery(BaseWorld world, vector from, vector to)
	{
		world.QueryEntitiesByLine(from, to, QueryTargetEntity);
	}

	// Internal query callback
	private bool QueryTargetEntity(IEntity ent)
	{
		if (CameraBase.Cast(ent))
			return true;

		m_aQueriedTargetEntities.Insert(ent);
		return true;
	}

	//! Returns first hit entity or null if none
	IEntity GetFirstEntity()
	{
		if (m_aQueriedTargetEntities.Count() > 0)
			return m_aQueriedTargetEntities[0];

		return null;
	}

	// Returns closest entity relative to point
	IEntity GetClosestEntity(vector relativeTo)
	{
		IEntity closest = null;
		float sqDistance = float.MAX;
		foreach (IEntity ent : m_aQueriedTargetEntities)
		{
			float currentSqDist = vector.DistanceSq(ent.GetOrigin(), relativeTo);
			if (currentSqDist < sqDistance)
			{
				closest = ent;
				sqDistance = currentSqDist;
			}
		}

		return closest;
	}
};
#endif
